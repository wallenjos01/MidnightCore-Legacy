plugins {

    id 'com.github.johnrengelman.shadow' version '7.1.2'

}

configurations {
    shade
}

shadowJar {

    archiveClassifier.set('')
    configurations = [project.configurations.shade]
}
tasks.assemble.dependsOn tasks.shadowJar

repositories {

    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {

    compileOnly "com.velocitypowered:velocity-api:${project.velocity_version}"
    compileOnly 'io.netty:netty-all:4.1.85.Final'

    implementation 'org.wallentines:midnightlib:1.0-SNAPSHOT'
    implementation project(':api')
    implementation project(':common')

    //shade 'org.wallentines:midnightlib:1.0-SNAPSHOT'

    shade project(':api')
    shade project(':common')

}

processResources {

    Map<String, ?> ex = new HashMap<>()
    ex.put("version", project.version)
    ex.put("id", project.mod_id)
    ex.put("name", project.mod_name)
    ex.put("target", project.name)
    ex.put("description", project.description)
    ex.put("group", project.group)
    ex.put("minecraftVersion", project.velocity_version)
    ex.put("mainClass", project.main_class_name)

    filesMatching("velocity-plugin.json") {
        expand(ex)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "${project.group}.${project.mod_id}"
        }
    }
    repositories {
        maven {
            if (project.hasProperty("pubUrl")) {
                url = project.pubUrl as String
                credentials {
                    username project.pubUser as String
                    password project.pubPass as String
                }
            }
        }
    }
}

